#version 330

flat in vec3 gs_objectPosition;
// Contains voxel-code, material-code and mip-map level
flat in ivec3 gs_voxel_material_mipmap;
flat in vec3 gs_objectNormal;
in vec3 gs_texCoord;
out vec4 fragColor;
uniform isampler2DArray u_componentTex;

#include "material.glsl"
#include "globalubo.glsl"

// Standard linear congruential generator to hash an integer
int hash(int i)
{
	return i * 1103515245 + 12345;
}

layout(std140) uniform Object
{
	mat4 c_mWorldView;
	mat4 c_mInverseWorldView;
	vec4 c_vCorner000;
	vec4 c_vCorner001;
	vec4 c_vCorner010;
	vec4 c_vCorner011;
	vec4 c_vCorner100;
	vec4 c_vCorner101;
	vec4 c_vCorner110;
	vec4 c_vCorner111;
	float c_fMaxOffset;
};

void main()
{
	// Determine which voxels can be sampled at all (dependent on neighborhood)
	int voxelMask = 0x40 | ((~gs_voxel_material_mipmap.x) & 0x3f);
	int textureIndex = 0;
	if( (gs_voxel_material_mipmap.x & 0x80000000) == 0 )
		textureIndex = gs_voxel_material_mipmap.y;
	ivec2 codes;
	codes.x = gs_voxel_material_mipmap.y;

	// Find view direction in component-space
	vec3 chunkSpacePos = gs_objectPosition + gs_texCoord - 0.5;
	vec3 viewDir = normalize((vec4(chunkSpacePos, 1) * c_mWorldView).xyz);
	vec3 chunkSpaceDir = viewDir * mat3x3(c_mInverseWorldView);
	int rx = (gs_voxel_material_mipmap.x & 0x03000000) >> 24;
	int ry = (gs_voxel_material_mipmap.x & 0x0c000000) >> 26;
	int rz = 3 - (rx + ry);
	bool toggleSignX = (gs_voxel_material_mipmap.x & 0x10000000) != 0;
	bool toggleSignY = (gs_voxel_material_mipmap.x & 0x20000000) != 0;
	bool toggleSignZ = (gs_voxel_material_mipmap.x & 0x40000000) != 0;
	vec3 componentSpaceDir;
	componentSpaceDir.x = toggleSignX ? -chunkSpaceDir[rx] : chunkSpaceDir[rx];
	componentSpaceDir.y = toggleSignY ? -chunkSpaceDir[ry] : chunkSpaceDir[ry];
	componentSpaceDir.z = toggleSignZ ? -chunkSpaceDir[rz] : chunkSpaceDir[rz];
	
	// Also rotate voxelMask to component-space
	voxelMask = 0x40 |
			((voxelMask & (1<<(rx*2+(toggleSignX?1:0)))) != 0 ? 0x01 : 0x00) |
			((voxelMask & (1<<(rx*2+(toggleSignX?0:1)))) != 0 ? 0x02 : 0x00) |
			((voxelMask & (1<<(ry*2+(toggleSignY?1:0)))) != 0 ? 0x04 : 0x00) |
			((voxelMask & (1<<(ry*2+(toggleSignY?0:1)))) != 0 ? 0x08 : 0x00) |
			((voxelMask & (1<<(rz*2+(toggleSignZ?1:0)))) != 0 ? 0x10 : 0x00) |
			((voxelMask & (1<<(rz*2+(toggleSignZ?0:1)))) != 0 ? 0x20 : 0x00);
	
	// Number of pixel must be equal in all directions (can theoretically be changed
	// if required). Used for ray marching the texture.
	int texSize = 16 / (1 << gs_voxel_material_mipmap.z);
	
	// Initialize ray marching algorithm
	ivec3 dirSign; 
	dirSign.x = componentSpaceDir.x < 0.0f ? -1 : 1;
	dirSign.y = componentSpaceDir.y < 0.0f ? -1 : 1;
	dirSign.z = componentSpaceDir.z < 0.0f ? -1 : 1;
	// Dependent on rotation change start position
	vec3 texPos;
	texPos.x = toggleSignX ? 1.0 - gs_texCoord[rx] : gs_texCoord[rx];
	texPos.y = toggleSignY ? 1.0 - gs_texCoord[ry] : gs_texCoord[ry];
	texPos.z = toggleSignZ ? 1.0 - gs_texCoord[rz] : gs_texCoord[rz];
	vec3 voxelPos = texPos * (texSize - 0.00001);
	ivec3 rayPos = ivec3(voxelPos);
	vec3 absDir = abs(componentSpaceDir);
	vec3 projLength = 1.0 / (absDir + 0.0001);
	vec3 d = voxelPos - rayPos;
	if(dirSign.x == 1) d.x = 1-d.x;
	if(dirSign.y == 1) d.y = 1-d.y;
	if(dirSign.z == 1) d.z = 1-d.z;
	d *= projLength;
	
	if( textureIndex != 0 && texSize > 1 )
	{
		// Ray march until a voxel is set in the current configuration
		codes = texelFetch(u_componentTex, ivec3(rayPos.x + rayPos.y * texSize, rayPos.z, textureIndex), gs_voxel_material_mipmap.z).xy;
		while((codes.y & voxelMask) == 0)
		{
			if(d.x < d.y || d.z < d.y) {
				if(d.x < d.z) {
					rayPos.x += dirSign.x;
					d.yz -= d.x;
					d.x = projLength.x;
				} else {
					rayPos.z += dirSign.z;
					d.xy -= d.z;
					d.z = projLength.z;
				}
			} else {
				rayPos.y += dirSign.y;
				d.xz -= d.y;
				d.y = projLength.y;
			}
			if(any(greaterThanEqual(rayPos, ivec3(texSize,texSize,texSize))) || any(lessThan(rayPos, ivec3(0,0,0))))
				discard;
			codes = texelFetch(u_componentTex, ivec3(rayPos.x + rayPos.y * texSize, rayPos.z, textureIndex), gs_voxel_material_mipmap.z).xy;
		}
	}
	
	// Estimate normal from the plane we intersected
	vec3 normalModification = d;
	vec3 normal = vec3(0.0, 0.0, 1.0);
	if(texSize <= 1) {
		normal = normalize(gs_objectNormal * mat3(c_mWorldView));
	} else {
		d = abs(d - projLength);
		if(d.x < 0.0001) normal = (toggleSignX ? dirSign.x : -dirSign.x) * vec3(c_mWorldView[0][rx], c_mWorldView[1][rx], c_mWorldView[2][rx]);
		if(d.y < 0.0001) normal = (toggleSignY ? dirSign.y : -dirSign.y) * vec3(c_mWorldView[0][ry], c_mWorldView[1][ry], c_mWorldView[2][ry]);
		if(d.z < 0.0001) normal = (toggleSignZ ? dirSign.z : -dirSign.z) * vec3(c_mWorldView[0][rz], c_mWorldView[1][rz], c_mWorldView[2][rz]);
		normal = normalize(normal);
	}
	normalModification = (0.05 - normalModification / projLength * 0.1) * dirSign;
	vec3 normalTmp;
	normalTmp[rx] = toggleSignX ? -normalModification.x : normalModification.x;
	normalTmp[ry] = toggleSignY ? -normalModification.y : normalModification.y;
	normalTmp[rz] = toggleSignZ ? -normalModification.z : normalModification.z;
	normal = normalize(normal + normalTmp);
	
	// Decode transparency or color rotation
	float yvar = float((codes.x >> 28) & 0xf) / 15.0;

	// Decode rgb color from material
	float color_y  = float(codes.x & 0xff) / 255.0;
	
	float phase = mod(hash(rayPos.x + rayPos.y * 8 + rayPos.z * 64), 6283) * 0.001;
	color_y *= (sin(c_fTime * 3.0 + phase) * 0.5 + 0.5) * yvar + 1 - yvar;
	float color_pb = (float((codes.x >> 18) & 0x1f) - 15.0) / 255.0;
	float color_pr = (float((codes.x >> 23) & 0x1f) - 15.0) / 255.0;
	vec3 color = vec3(color_y + 0.22627 * color_pb + 11.472 * color_pr,
					color_y - 3.0268 * color_pb - 5.8708 * color_pr,
					color_y + 14.753 * color_pb + 0.0082212 * color_pr);
	//color = min(vec3(1,1,1), max(vec3(0,0,0), color));

	// Decode other material parameters
	float shininess = float((codes.x >> 12) & 0xf) / 15.0;
	// Use code as power of to and compute: (2^spec) * 3
	float specular = (1 << ((codes.x >> 8) & 0xf)) * 3.0;
	float emissive = float((codes.x >> 17) & 1);
	
	fragColor.xyz = Lightning(normal, -viewDir, shininess, specular, color, emissive);
	fragColor.a = 1;
}
